plugins {
    id 'dev.architectury.loom' version '0.8.0-SNAPSHOT' apply false
    id 'java'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'dev.architectury.loom'

    sourceCompatibility = JavaVersion.toVersion(rootProject.mod_java_version)
    targetCompatibility = JavaVersion.toVersion(rootProject.mod_java_version)

    buildDir = rootDir.absolutePath + "/build/$project.name"
    archivesBaseName = rootProject.archives_base_name
    version = rootProject.mod_version
    group = rootProject.maven_group

    sourceSets {
        main {
            java {
                srcDirs([
                        "src/barrel/java",
                        "src/base/java",
                        "src/chest/java",
                        "src/old_chest/java",
                        rootDir.toPath().resolve("common/${project.name}Src/barrel/java"),
                        rootDir.toPath().resolve("common/${project.name}Src/base/java"),
                        rootDir.toPath().resolve("common/${project.name}Src/chest/java"),
                        rootDir.toPath().resolve("common/${project.name}Src/old_chest/java")
                ])
            }
            resources {
                srcDirs([
                        "src/barrel/resources",
                        "src/base/resources",
                        "src/chest/resources",
                        "src/old_chest/resources",
                        "src/common/resources",
                        rootDir.toPath().resolve("common/${project.name}Src/barrel/resources"),
                        rootDir.toPath().resolve("common/${project.name}Src/base/resources"),
                        rootDir.toPath().resolve("common/${project.name}Src/chest/resources"),
                        rootDir.toPath().resolve("common/${project.name}Src/old_chest/resources"),
                        rootDir.toPath().resolve("common/${project.name}Src/common/resources"),
                        rootDir.toPath().resolve("common/${project.name}Src/chest_compat/resources")
                ])
            }
        }
    }

    loom {
        silentMojangMappingsLicense()
        useFabricMixin = true
    }

    dependencies {
        minecraft("com.mojang:minecraft:${rootProject.minecraft_version}")
        mappings loom.officialMojangMappings()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    remapJar {
        archiveFileName = "$archivesBaseName-$mod_version+$minecraft_version-fat.jar"
    }

    jar {
        archiveFileName = "$archivesBaseName-$mod_version+$minecraft_version-dev.jar"
    }

    task minJar(type: Jar) {
        dependsOn(tasks.remapJar)
        def inputs = zipTree(remapJar.outputs.files.singleFile)
        from inputs
        archiveFileName = "$archivesBaseName-$mod_version+${minecraft_version}.jar"
        def transformer = new Transformer<String, String>() {
            def lines = new StringBuilder()
            int openBrackets = 0
            def slurper = Class.forName('groovy.json.JsonSlurper').getDeclaredConstructor().newInstance()
            def toJson = Class.forName('groovy.json.JsonOutput').getDeclaredMethod('toJson', Map.class)
            @Override
            String transform(String line) {
                if (line != "") {
                    affectOpenBrackets(line)
                    lines.append(line)
                    if (openBrackets == 0) {
                        var json = lines.toString()
                        lines = new StringBuilder()
                        return toJson.invoke(null, slurper.parseText(json))
                    }
                }
                return null
            }

            void affectOpenBrackets(String line) {
                openBrackets += line.count('{') - line.count('}')
            }
        }
        filesMatching(['**/*.json', '**/*.mcmeta']) {
            it.filter(transformer)
        }
    }
}
