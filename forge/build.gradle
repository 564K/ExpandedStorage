plugins {
    id 'dev.architectury.loom' // Must be before architectury-plugin
    id 'architectury-plugin'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

architectury {
    forge()
    injectInjectables = false
}

loom {
    silentMojangMappingsLicense()
    useFabricMixin = true
}

configurations {
    shadowCommon // Don't use shadow from the shadow plugin because we don't want IDEA to index this.
}

repositories {
    maven {
        // JEI maven
        name "Progwml6 maven"
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        // JEI maven - fallback
        name "ModMaven"
        url "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
    mappings loom.officialMojangMappings()

    forge "net.minecraftforge:forge:${rootProject.minecraft_version}-${rootProject.forge_version}"

    // For all modules
    modCompileOnly "mezz.jei:jei-${rootProject.minecraft_version}:${project.jei_version}:api"
    modRuntime "mezz.jei:jei-${rootProject.minecraft_version}:${project.jei_version}"

    implementation(project(path: ':common')) {
        transitive = false
    }
    developmentForge(project(path: ':common')) {
        transitive = false
    }
    shadowCommon(project(path: ':common', configuration: 'transformProductionForge')) {
        transitive = false
    }
}

processResources {
    def props = ['version': project.version]
    inputs.properties props

    filesMatching("META-INF/mods.toml") {
        expand props
    }
}

shadowJar {
    exclude 'fabric.mod.json'

    configurations = [project.configurations.shadowCommon]
    classifier 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    classifier 'forge'
}

jar {
    classifier 'dev'
}

sourcesJar {
    def commonSources = project(':common').sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}
